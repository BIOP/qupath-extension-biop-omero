plugins {
    // To create a shadow/fat jar, including dependencies
    //id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
    id 'java-library'
    alias(libs.plugins.javafx)
}

ext.moduleName = 'qupath.extension.biop.omero'
ext.qupathVersion = gradle.ext.qupathVersion

archivesBaseName = 'qupath-extension-biop-omero'
description = "QuPath extension to support image reading using OMERO's Java API."
group = "ch.epfl.biop"
version = "1.0.0-SNAPSHOT"

repositories {
  maven{
        url "https://maven.scijava.org/content/groups/public"
  }
    maven{
        url "https://artifacts.igred.fr/releases"
   }
}

configurations {
    implementation.extendsFrom bioformats
}

dependencies {
    //implementation libs.commons.text
   // implementation "commons-io:commons-io:2.11.0"
    implementation "io.github.qupath:qupath-fxtras:0.1.0"
    implementation "io.github.qupath:qupath-core:${qupathVersion}"
    implementation "io.github.qupath:qupath-gui-fx:${qupathVersion}"

    implementation "org.openmicroscopy:omero-gateway:5.9.0"
    implementation "fr.igred:simple-omero-client:5.16.0"

    //testImplementation libs.junit
    //testRuntimeOnly libs.junit.engine
}

processResources {
  from ("${projectDir}/LICENSE.txt") {
    into 'licenses/'
  }
}

tasks.register("copyDependencies", Copy) {
  description "Copy dependencies into the output libs directory"
  group "QuPath"
  
  from configurations.default
  into 'build/libs'
}

java {
    def version = project.findProperty('toolchain')
    if (!version)
        version = libs.versions.jdk.get()
    else if (version.strip() == 'skip')
        version = null
    if (version != null) {
        logger.quiet("Setting toolchain to {}", version)
        toolchain {
            languageVersion = JavaLanguageVersion.of(version)
        }
    }
}

javadoc {
    options.addBooleanOption('html5', true)
    destinationDir = new File(project.rootDir,"docs")
}

jar {
    // Important to set version so this can be queried within QuPath
    manifest {
        def manifestAttributes = [
                "Implementation-Vendor": "QuPath developers",
                "Implementation-Version": project.version,
                //               'Automatic-Module-Name': "io.github." + moduleName,
                "QuPath-build-time": new Date().format("yyyy-MM-dd, HH:mm")
        ]

        attributes(manifestAttributes)
    }
}
publishing {

    repositories {
        maven {
            name = "scijava"
            //credentials(PasswordCredentials::class)
            if (version.toString().endsWith("SNAPSHOT")) {
                url = uri("https://maven.scijava.org/content/repositories/snapshots")
            } else {
                url = uri("https://maven.scijava.org/content/repositories/releases")
            }
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
            }
        }
    }
}
